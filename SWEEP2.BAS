1 'MINESWEEPER TEXT TEST
3 CLEAR 200,9999'SET HIMEM
5 ?"START:";(PEEK(25)*256+PEEK(26))'START ADDR
6 ?"END:";(PEEK(27)*256+PEEK(28))'END ADDR
7 ?"ARRAY START: ";PEEK(29)*256+PEEK(30)
8 ?"ARRAY END: ";(PEEK(31)*256+PEEK(32))+1
9 ?"STRING START: ";PEEK(33)*256+PEEK(34)
10 ?"HIMEM: ";(PEEK(35)*256+PEEK(36))+1
11 EXEC 44539
12 'ORIGINAL BOARD:
13 'NO MINE=0
14 'MINE=-1
15 'VISITED=-2
16 'UNCALCULATED=-3
17 'PLAYER BOARD:
18 'FLAG=-3
19 'UNREVEALED=-4
21 MC=10'MINE COUNT
25 SZ=9'BOARD SIZE
30 TL=((SZ+1)*(SZ+1))-MC'TILES LEFT TO CLEAR
35 DIM OB(SZ,SZ)'ORIGINAL BOARD
40 DIM PB(SZ,SZ)'PLAYER BOARD
45 DIM ST(SZ*SZ)'STACK
50 GOSUB 5000'TITLE SCREEN
55 CLS:? "LOADING ASM ROUTINES..."
60 GOSUB 3000
90 ?"GENERATING BOARD...";
100 GOSUB 1000'CREATE BOARD
110 POKE 275,39:POKE 276,17'STACK.SINIT
120 A=USR(10250)'SET STACK ADDR
200 'MAINLOOP
210 REM* CLS
215 GOSUB 2100'PRINT PLAYER BOARD
220 PRINT "MOVE? (F)LAG (R)EVEAL (D)EBUG"
230 INPUT A$
240 IF A$="D" THEN GOSUB 2000:INPUT A$:GOTO 210
250 PRINT "X COORD?":INPUT X1:PRINT "Y COORD?":INPUT Y1
255 IF X1<0 OR X1>SZ OR Y1<0 OR Y1>SZ THEN PRINT "INVALID":GOTO 810
260 IF A$="F" THEN GOSUB 900
270 IF A$="R" THEN GOSUB 800
280 GOTO 210
499 END
800 REM* SUB: REVEAL
810 IF OB(X1,Y1)=-1 THEN GOTO 1300'HIT MINE: GAME OVER
820 PB(X1,Y1)=OB(X1,Y1)
825 TL=TL-1'DECREASE MINE COUNT
830 IF OB(X1,Y1)=0 THEN GOSUB 1500'CLEAR ADJACENT ZEROS
840 IF TL=0 THEN 1400'WIN
890 RETURN
900 REM* 'SUB: FLAG
910 IF PB(X1,Y1)<>-3 AND PB(X1,Y1)<>-4 THEN PRINT "INVALID SELECTION":INPUT A$:GOTO 990
930 IF PB(X1,Y1)=-3 THEN PB(X1,Y1)=-4 ELSE PB(X1,Y1)=-3'TOGGLE
990 RETURN
1000 REM* SUB: CREATE BOARDS
1005 FOR X=0 TO SZ:FOR Y=0 TO SZ
1010 OB(X,Y)=-3'DEFAULT STATE FOR OB
1013 PB(X,Y)=-4'MARK UNVISITED
1015 NEXT Y,X
1020 I=MC'NUMBER OF MINES LEFT TO PLACE
1025 X=RND(SZ):Y=RND(SZ)
1030 IF OB(X,Y)=-1 THEN GOTO 1025
1040 OB(X,Y)=-1
1050 I=I-1
1055 PRINT ".";
1060 IF I>0 THEN GOTO 1025
1100 'CALCULATE DISTANCES
1110 FOR X=0 TO SZ:FOR Y=0 TO SZ
1115 ?".";
1120 IF OB(X,Y)<>-3 THEN 1190
1130 OB(X,Y)=0
1140 FOR I=-1 TO 1:FOR J=-1 TO 1
1150 IF Y+J<0 OR Y+J>SZ THEN 1180
1160 IF X+I<0 OR X+I>SZ THEN 1180
1170 IF OB(X+I,Y+J)=-1 THEN OB(X,Y)=OB(X,Y)+1
1180 NEXT J
1185 NEXT I
1190 NEXT Y
1195 NEXT X
1210 RETURN
1300 REM* GAME OVER
1310 ?"GAME OVER"
1315 GOSUB 2000
1320 END
1400 REM* WIN
1410 ?"YOU WON!"
1440 END
1500 REM* SUB: CLEAR ADJACENT EMPTY TILES CENTERED ON X1,Y1
1520 POKE 275,39:POKE 276,19'STACK.PUSH
1528 IF OB(X1,Y1)=-5 THEN 1560
1529 OB(X1,Y1)=-5:PB(X1,Y1)=0
1532 FOR I=-1 TO 1
1533 FOR J=-1 TO 1
1534 X=X1+I:Y=Y1+J
1535 IF X<0 OR X>SZ THEN 1545
1536 IF Y<0 OR Y>SZ THEN 1540
1538 IF OB(X,Y)=0 THEN A=USR(X1):A=USR(Y1):A=USR(I):A=USR(J):X1=X:Y1=Y:GOSUB 1520:GOSUB 1570 ELSE IF OB(X,Y)<>-5 THEN GOSUB 1600
1540 NEXT J
1545 NEXT I
1550 REM* PLACEHOLDER
1560 RETURN
1570 REM* SUB: POP OFF STACK
1575 POKE 275,39:POKE 276,21'STACK.SPOP
1580 J=USR(0)
1583 I=USR(0)
1585 Y1=USR(0)':?"POP: ";Y1
1586 X1=USR(0)':?"POP: ";X1
1587 ?"POP: ";X1;Y1;I;J
1595 RETURN
1600 REM* SUB: COPY OVER CELL
1610 PB(X1+I,Y1+J)=OB(X1+I,Y1+J)
1620 OB(X1+I,Y1+J)=-5
1630 RETURN
2000 REM* SUB: PRINT ORIGINAL BOARD
2005 ?"   0123456789"
2010 ?"   ----------"
2015 FOR J=0 TO SZ
2020 PRINT J;
2025 FOR I=0 TO SZ
2030 IF OB(I,J)=-1 THEN PRINT "*";:GOTO 2050'MINE
2035 IF OB(I,J)=-3 THEN PRINT "U";:GOTO 2050'UNCALCULATED
2040 IF OB(I,J)=-2 THEN PRINT "V";:GOTO 2050'VISITED
2043 IF OB(I,J)=-5 THEN PRINT "C";:GOTO 2050'CLEARED
2045 PRINT RIGHT$(STR$(OB(I,J)),1);
2050 NEXT I
2060 ?""
2070 NEXT J
2075 ?"TL=";TL
2080 RETURN
2100 REM* SUB: PRINT PLAYER BOARD
2110 ?"   0123456789"
2120 ?"   ----------"
2170 FOR J=0 TO SZ
2175 PRINT J;
2180 FOR I=0 TO SZ
2190 IF PB(I,J)=-3 THEN PRINT "F";:GOTO 2220
2200 IF PB(I,J)=-4 THEN PRINT "#";:GOTO 2220
2210 PRINT RIGHT$(STR$(PB(I,J)),1);
2220 NEXT I
2230 PRINT ""
2240 NEXT J
2250 RETURN
3000 REM* SUB: LOAD ASM ROUTINE
3010 FOR I=0 TO 72:READ B:POKE 10001+I,B:NEXT I'STACK
3011 DATA 32,4,32,12,32,40,189,179,237,237,140,59,237,140,54
3012 DATA 57,189,179,237,238,140,49,48,201,255,106,172,140,42,38
3013 DATA 5,204,255,255,32,7,54,4,79,95,239,140,28,126,180
3014 DATA 244,238,140,22,17,163,140,16,38,5,204,255,255,32,6
3015 DATA 55,4,29,239,140,5,126,180,244,0,0,0,0
3020 RETURN
5000 REM* SUB: DISPLAY TEXT-BASED TITLE SCREEN
5010 CLS
5020 ?"MINDSWEEPER V0.1"
5030 ?"LAUREN RAD 2024"
5040 ?"PRESS SPACEBAR TO START"
5050 A$=INKEY$
5060 IF A$<>" " THEN GOTO 5050
5070 CLS
5080 RETURN
5999 REM DUMMY
